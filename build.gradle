import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    repositories {
        maven {
            url "https://maven.aliyun.com/repository/public"
        }
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.1.6.RELEASE")
    }
}

allprojects {
    repositories {
        maven {
            url "https://maven.aliyun.com/repository/public"
        }
        jcenter()
        mavenCentral()
    }

    ext {
        guavaVersion = "28.0-jre"

        // Apache Commons
        commonsIoVersion = '2.6'
        commonsLangVersion = '3.9'
        commonsTextVersion = '1.6'
        commonsCodecVersion = '1.12'
        commonsFileuploadVersion = '1.4'
        commonsBeanutilsVersion = '1.9.3'
        commonsCollectionsVersion = '4.2'
        commonsCompressVersion = '1.18'
        commonsMailVersion = '1.5'
    }

    apply plugin: 'io.spring.dependency-management'

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }
}

project("webapp") {
    apply plugin: 'idea'
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'

    group = 'cn.elvea.lxp'
    version = '1.0.0'
    sourceCompatibility = '11'

    repositories {
        mavenCentral()
    }

    configurations {
        compile.exclude module: 'spring-boot-starter-logging'
        compile.exclude group: 'junit'

        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-log4j2'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        // lombok
        implementation 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        // commons
        implementation group: 'org.apache.commons', name: 'commons-text', version: commonsTextVersion
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: commonsLangVersion
        implementation group: 'org.apache.commons', name: 'commons-collections4', version: commonsCollectionsVersion
        // 3rd party
        implementation 'joda-time:joda-time'
        implementation 'mysql:mysql-connector-java'
        implementation group: 'com.google.guava', name: 'guava', version: guavaVersion
        // development 
        developmentOnly("org.springframework.boot:spring-boot-devtools")
        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testImplementation 'org.springframework.security:spring-security-test'
        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
    }
}
